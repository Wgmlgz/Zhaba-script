use range
use operators
use out

type Vec T: int size; 
  int capacity
  TP head

type VecIter T: TP ptr

impl VecIter
  fn ctor TP ptr: VecIter<T> slf; slf.ptr = ptr; <<< slf
  fn next: slf.ptr = slf.ptr + 1
  fn bool uneq VecIter<T> other: <<< slf.ptr != other.ptr
  lop TR * VecIter<T> slf: <<< *slf.ptr

impl Vec
  fn TP atP int pos: <<< (slf.head + pos) as TP
  fn TR at int pos: <<< *slf.atP(pos)
    
  fn print
    put '['
    @ i 0..slf.size
      ? ! !i: put ' '
      put (*slf.atP(i))
    put ']' 

  fn double_capacity
    new_capacity := slf.capacity
    ? new_capacity == 0
      new_capacity = 1
      slf.head = malloc(sizeof T) as TP
      slf.capacity = new_capacity
    \ new_capacity *= 2
      new_head := malloc(new_capacity * sizeof T) as TP
      @ i 0..slf.size: *(new_head + i) = slf.at(i)
      free(slf.head as int)
      slf.head = new_head
    slf.capacity = new_capacity

  fn push_back T val
    ? slf.size == slf.capacity: slf.double_capacity()
    slf.at(slf.size) = val
    ++slf.size
    
  fn pop_back: --slf.size
  
  fn VecIter<T> begin: <<< VecIter<T>(slf.head) 
  fn VecIter<T> end: <<< VecIter<T>(slf.atP(slf.size))
  fn TR front: <<< *slf.head
  fn TR back: <<< *slf.atP(slf.size-1)

  fn dtor
    ? ! !(slf.head as int)
      free(slf.head as int)
      slf.size = 0
      slf.capacity = 0
      slf.head = 0 as TP

  /** Constructors */
  fn ctor int size
    Vec<T> slf
    slf.head = 0 as TP
    slf.size = 0
    slf.capacity = 0
    @ slf.capacity < size: slf.double_capacity()
    slf.size = size
    slf.capacity = size
    <<< slf
  fn ctor int size T default
    slf := Vec<T>(size)
    @ i 0..slf.size: slf.at(i) = default
    <<< slf

  fn ctor: <<< Vec<T>(0)

  fn ctor Vec<T>R other
    slf := Vec<T>(other.size)
    @ i 0..other.size: slf.at(i) = other.at(i)
    <<< slf

  /**  Overloaded operators */
  op 16 += Vec<T>R slf T val: slf.push_back(val)
  
  op 16 += Vec<T>R slf Vec<T>R other
    @ i other: slf.push_back(*i)

  lop put Vec<T>R slf: slf.print()
  lop out Vec<T>R slf: slf.print(); out ''


  rop Out < Vec<T>R i: out i
    <<< _Out()
  lop Out < Vec<T>R i: put i, put ' '
    <<< _Out()
  op 9 Out < Out o Vec<T>R i: put i, put ' '
    <<< _Out()

  fn Vec<T> sub int begin int end
    res := Vec<T>(0)
    @ i begin..end: res.push_back(slf.at(i))
    <<< res

  lop swap TP a TP b: T t = *a; *a = *b; *b = t

  lop TP partition TP low TP high
    pivot := high - 1 i := low j := low
    @ j = low j != pivot j = j + 1
      ? *j < *pivot: swap(i j); i = i + 1
    swap(i  pivot)
    <<< i

  lop qsort TP lhs TP rhs
    a := rhs as int - lhs as int
    b := sizeof(T)
    ? a > b
      pi := partition(lhs rhs)
      qsort(lhs pi)
      pi = pi + 1
      qsort(pi rhs)
  
  fn sort
    begin := slf.begin()
    end := slf.end()
    qsort(begin.ptr end.ptr)
  lop sort Vec<T>R slf: slf.sort()

fn Vec<int> iota int begin int end
  v := Vec<int>(end - begin)
  @ i begin..end: v.at(i - begin) = i
  <<< v


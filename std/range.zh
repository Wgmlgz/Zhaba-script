/**
 * Range type for loops or something
 * example:
 * @ i 2..5: out i
 */

use operators
use util

type Range T: T begin end

type RangeIter T: T val

type RangeIterRange T: RangeIter<T> begin end 

impl RangeIter
  fn ctor T val
    RangeIter<T> slf
    slf.val = val
    <<< slf
  lop ++ RangeIter<T>R slf
    ++slf.val
  lop T * RangeIter<T> slf: <<< slf.val
  op 10 bool != RangeIter<T> a RangeIter<T> b: <<< a.val != b.val


impl RangeIterRange
  fn RangeIter<T> begin: <<< slf.begin
  fn RangeIter<T> end: <<< slf.end
  fn ctor RangeIter<T> begin RangeIter<T> end
    RangeIterRange<T> slf
    slf.begin = begin
    slf.end = end
    <<< slf


impl Range
  fn T begin: <<< slf.begin
  fn T end: <<< slf.end
  fn ctor T begin T end
    Range<T> slf
    slf.begin = begin
    slf.end = end
    <<< slf

  /**  begin..end */
  op 9 Range<T> .. T begin T end: <<< Range<T>(begin end)
  op 9 Range<T> ..- T begin T end: <<< begin.. -end

  /**  begin.. */
  rop Range<T> .. T begin: <<< begin..(~T())
  /**  ..end */
  lop Range<T> .. T end: <<< (++ ~T())..end
  /**  ..-end */
  lop Range<T> ..- T end: <<< (++ ~T()).. -end
  /**  -begin..end */
  lop Range<T> - Range<T> r: <<< (-r.begin)..r.end


  /**  begin..=end */
  op 9 Range<T> ..= T begin T end: <<< begin..(end+(++ T()))
  /**  ..=end */
  lop Range<T> ..= T end: <<< (++ ~T())..(end+(++ ~T()))
  /**  ..=-end */
  lop Range<T> ..=- T end: <<< (++ ~T())..(-end+(++ ~T()))


  /** T in range */
  op 10 bool == T i Range<T> r
    ? r.begin < r.end: <<< r.begin <= i && i < r.end
    \ <<< r.end <= i && i < r.begin

  lop RangeIterRange<T> iter Range<T> r
    <<< RangeIterRange<T>(RangeIter<T>(r.begin) RangeIter<T>(r.end))

fn rangeMaker
  Range<i8> a
  Range<i16> b
  Range<i32> c
  Range<i64> d
  Range<char> e

  RangeIterRange<i8> a1
  RangeIterRange<i16> b1
  RangeIterRange<i32> c1
  RangeIterRange<i64> d1
  RangeIterRange<char> e1

include 'std.zh'
include 'util.zh'
include './canvas.zh'

fn char aschar str s: <<< *(s as charP)

fn main
  auto w = 20 h = 20
  auto c = newCanvas(w h)
  auto r = 54
  int i j k
  auto dead = aschar('.') live = aschar('#')
  c.fill(aschar('.'))

  /* fill with random stuff */
  @ i=0 i<c.h i=i+1
    @ j=0 j<c.w j=j+1
      r = rng() as int
      ? r % 2
        *c.at(i j) = aschar('#')

  @ k=0 k<50 k=k+1
    auto nc = newCanvas(w h)
    nc.fill(aschar('.'))
    
    @ i=1 i<c.h-1 i=i+1
      @ j=1 j<c.w-1 j=j+1
        int near = 0
        ? c.get(i-1 j  ) == live: &near += 1
        ? c.get(i   j-1) == live: &near += 1
        ? c.get(i+1 j  ) == live: &near += 1
        ? c.get(i   j+1) == live: &near += 1
        ? c.get(i-1 j-1) == live: &near += 1
        ? c.get(i-1 j+1) == live: &near += 1
        ? c.get(i+1 j-1) == live: &near += 1
        ? c.get(i+1 j+1) == live: &near += 1

        ? *c.at(i j) == live
          ? near == 2 || near == 3: *nc.at(i j) = live as char
        \ ? near == 3: *nc.at(i j) = live as char

    c = nc

    out &c
    sleep 600
